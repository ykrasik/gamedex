apply plugin: 'com.github.ben-manes.versions'

// Easier to remember alias
task versions(dependsOn: dependencyUpdates)

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean notRelease = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (notRelease) {
                selection.reject('Release candidate')
            }

            boolean ignored = [
                'org.kordamp.ikonli:ikonli-javafx:11.*',
                'org.kordamp.ikonli:ikonli-javafx:12.*',
                'org.controlsfx:controlsfx:9.*',
                'org.controlsfx:controlsfx:11.*',
                'com.jfoenix:jfoenix:9.*',
                'org.fxmisc.richtext:richtextfx:0.*',
                'no.tornado:tornadofx:1.*',
                'com.miguelfonseca.completely:completely-core:0.9.*',
                'ch.qos.logback:logback-classic:1.4.*',
                'com.h2database:h2:2.*',
            ].any { regex ->
                selection.candidate.toString() ==~ regex
            }
            if (ignored) {
                selection.reject('Ignored')
            }
        }
    }
}
