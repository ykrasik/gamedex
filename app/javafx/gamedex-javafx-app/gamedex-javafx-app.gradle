apply plugin: 'application'

executableDir = ''
mainClassName = "com.gitlab.ykrasik.gamedex.javafx.Main"

startScripts {
    classpath = files('$APP_HOME/lib/*')
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

dependencies {
    fullDependency ":core:gamedex-core"
    fullDependency ":app:javafx:gamedex-javafx-view"

    testImplementation project(":core:gamedex-core-persistence")
    testImplementation project(":core:gamedex-core-provider")

    pluginProjects().forEach {
        testImplementation testFixtures(it)
    }
}

task copyPluginsDist(type: Copy) {
    from pluginProjects().shadowJar
    into new File(project.buildDir, "plugins")
}

javapackager {
    mainClass = mainClassName
    additionalResources = copyPluginsDist.outputs.files.files.asList() +
            new File(project.projectDir, "assets/GameDex.l4j.ini")
    bundleJre = true
    jrePath = new File(System.properties["java.home"])
    generateInstaller = project.hasProperty("installer")

    name = "GameDex"
    displayName = "GameDex"
    description = "GameDex"
    organizationName = "Yevgeny Krasik"
    url = "https://github.com/ykrasik/gamedex"

    winConfig {
        exeCreationTool = 'why'

        fileVersion = project.version
        productVersion = project.version

        generateSetup = true
        generateMsi = false

        disableWelcomePage = false
        disableDirPage = false
        disableProgramGroupPage = true
        disableFinishedPage = false
        disableRunAfterInstall = false
    }
}

project.tasks.getByName("package").dependsOn copyPluginsDist

// TODO: Temp hack until javapackager is released with the 'why' version where this is fixed.
task copyReleaseHack(type: Copy) {
    from new File(System.properties["java.home"], "../release")
    into new File(project.buildDir, "GameDex/jre")
}
project.tasks.getByName("package").finalizedBy copyReleaseHack

task dist(type: Copy, dependsOn: ['package', copyReleaseHack]) {
    from new File(project.buildDir, "GameDex")
    into new File(project.rootDir, "dist")
}

task cleanDist(type: Delete) {
    delete new File(project.rootDir, "dist")
}

clean.finalizedBy cleanDist

task runDemo(dependsOn: pluginProjects().shadowJar, type: JavaExec) {
    mainClass = 'com.gitlab.ykrasik.gamedex.javafx.TestApplication'
    classpath = sourceSets.test.runtimeClasspath
}

test.dependsOn pluginProjects().shadowJar
